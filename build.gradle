plugins {
    id 'groovy'
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.asseco-pst'
version = '0.0.3'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy:3.0.5'
    // https://mvnrepository.com/artifact/net.wasdev.maven.tools.targets/liberty-apis
    implementation 'net.wasdev.maven.tools.targets:liberty-apis:20.0.0.9'
    // https://mvnrepository.com/artifact/com.ibm.websphere.appserver.api/com.ibm.websphere.appserver.api.restConnector/1.3.44
    implementation 'com.ibm.websphere.appserver.api:com.ibm.websphere.appserver.api.restConnector:1.3.44'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'liberty'
            from components.java
            pom {
                name = 'liberty'
                description = 'A library that allows managing artifacts in a Liberty server'
                url = 'https://github.com/asseco-pst/liberty'
                inceptionYear = '2020'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'assecopst'
                        name = 'Asseco PST'
                        email = 'servicos.dcs@pst.asseco.com'
                        organization = 'Asseco PST'
                        organizationUrl = 'https://www.pst.asseco.com'
                    }
                }
                scm {
                    url = 'https://github.com/asseco-pst/liberty'
                    connection = 'scm:https://github.com/asseco-pst/liberty.git'
                    developerConnection = 'scm:git://github.com/asseco-pst/liberty.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}